selector = {"." ident | "[" expression "]"}.
factor = ident selector | integer | "(" expression ")" | "not" factor.
term = factor {("*" | "div" | "mod" | "and") factor}.
SimpleExpression = ["+" | "-"] term {("+" | "-" | "or") term}.
expression = SimpleExpression
  {("=" | "<>" | "<" | "<=" | ">" | ">=") SimpleExpression}.

assignment = ident selector ":=" expression.
ActualParameters = "(" [expression {"," expression}] ")".
ProcedureCall = ident selector [ActualParameters].
CompoundStatement(indent_0) = "begin" statement(indent_1) {";" statement} "end".
IfStatement = "if" expression "then" Statement ["else" Statement].
WhileStatement = "while" expression "do" Statement.
Statement = [assignment | ProcedureCall | CompoundStatement |
  IfStatement | WhileStatement].

IdentList = ident {"," ident}.
ArrayType = "array" "[" expression ".." expression "]" "of" type.
FieldList = [IdentList ":" type].
RecordType = "record" FieldList {";" FieldList} "end".
type = ident | ArrayType | RecordType.
FPSection = ["var"] IdentList ":" type.
FormalParameters = "(" [FPSection {";" FPSection}] ")".
ProcedureDeclaration(indent_0) = "procedure" ident [FormalParameters] ";"
  declarations(indent_1) CompoundStatement(indent_1).
declarations(indent_0) = ["const" {ident "=" expression(indent_1) ";"}]
  ["type" {ident(indent_1) "=" type ";"}]
  ["var" {IdentList(indent_1) ":" type ";"}]
  {ProcedureDeclaration ";"}.

program = "program" ident ["(" ident {"," ident} ")"] ";"
  declarations CompoundStatement.


# NOTE: The single attribute rule is "indent_(n + 1) = indent_n + 1"
